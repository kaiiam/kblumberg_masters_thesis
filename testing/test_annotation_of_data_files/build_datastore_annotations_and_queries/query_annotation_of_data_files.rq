PREFIX obo: <http://purl.obolibrary.org/obo/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 

SELECT DISTINCT ?c 
	(group_concat(?value; separator=" , ") as ?values)

FROM <datastore.ttl>
WHERE {

{
#get me some thing which is a data matrix about seawater
?c rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:first obo:OBCS_0000120 ; 
   rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:onProperty obo:IAO_0000136 ;
} 
UNION
{
#get me a something (a column) which is part of some data matrix and is about some value, and return me that value
?c rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:first/owl:onProperty obo:BFO_0000050 ;
   rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:first/owl:someValuesFrom obo:OBCS_0000120 ;
   rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:onProperty obo:IAO_0000136 ;
}

   ## Or stament to access annotations of various property paths:
   # starts from: rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom
?c (
   # X in (X)
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom) |
   # X1-Xn in (X1 or X2 or ...)
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:unionOf/rdf:rest*/rdf:first) | 
   # X1-Xn in (X1 and X2 and ...)
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first) |
   # X1-Xn in ((X1 or X2 or ...) and ... )
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:first/owl:unionOf/rdf:rest*/rdf:first) |
   # Y and Z in (X1 and X2 ... and ('any property' some Y and 'any property' some Z))
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first/owl:intersectionOf/rdf:rest*/rdf:first/owl:someValuesFrom) |
   # Y in (X1 and X2 ... and ('any property' some Y))
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first/owl:someValuesFrom) |
   # Y1-Yn in (X1 and X2 ... and ('any property' some (Y1 and Y2 and ... )))
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first) |
   # Y1-Yn in (X1 and X2 ... and ('any property' some (Y1 or Y2 or ... )))
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first/owl:someValuesFrom/owl:unionOf/rdf:rest*/rdf:first) |
   # Z in (X1 and X2 ... and ('any property' some (Y1 and Y2 and ... and ('any property' some Z))))
   (rdf:type/owl:equivalentClass/owl:intersectionOf/rdf:rest/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first/owl:someValuesFrom/owl:intersectionOf/rdf:rest*/rdf:first/owl:someValuesFrom)

												) ?value .



#VALUES (?value) {
#(<http://purl.obolibrary.org/obo/ENVO_03000002>)
#(<http://purl.obolibrary.org/obo/ENVO_03000000>)
#(<http://purl.obolibrary.org/obo/PATO_0000915>)
#(<http://purl.obolibrary.org/obo/BFO_0000021>)
#(<http://purl.obolibrary.org/obo/ENVO_00002149>)
#(<http://purl.obolibrary.org/obo/PATO_0000033>)
#(<http://purl.obolibrary.org/obo/CHEBI_18230>)
#(<http://purl.obolibrary.org/obo/ENVO_09200006>)
#(<http://purl.obolibrary.org/obo/PATO_0001595>)
#(<http://purl.obolibrary.org/obo/ENVO_01001037>)
#}

} GROUP BY ?c
